plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "1.3.4"
    id("xyz.jpenilla.run-paper") version "1.0.6"
    id("net.minecrell.plugin-yml.bukkit") version "0.5.1"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "io.freefair.lombok" version "6.3.0"

    id "maven-publish"
}

group 'eu.battleland.crownedbank'
version '1.0-dev'

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    // in-house
    implementation(project(":api"))

    // cloud
    implementation 'cloud.commandframework:cloud-paper:1.7.0'
    implementation 'cloud.commandframework:cloud-brigadier:1.7.0'

    // papi
    compileOnly 'me.clip:placeholderapi:2.11.2'

    // paperweight
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.18.1-R0.1-SNAPSHOT")
}


shadowJar {
    archiveFileName = "crownedbank-paper.jar"
    relocate 'cloud.commandframework', 'eu.battleland.crownedbank.libs.cloud.commandframework'
}
tasks.named("build") { dependsOn(tasks.named("shadowJar")) }

bukkit {
    main = "eu.battleland.crownedbank.paper.BankPlugin"
    name = "CrownedBankPaper"
    apiVersion = "1.18"
    permissions {
        'crownedbank.admin' {
            description = 'Administrator permission.'
            setDefault('OP')
        }
        'crownedbank.player' {
            description = 'Player permission.'
            setDefault('TRUE')
        }
    }
}

publishing {
    publications {
        gpr(MavenPublication) {
            artifactId = 'crowned-bank-paper'
            from components.java
        }
    }
    publishing {
        repositories {
            maven {
                name = "central"
                url = uri("https://nexus.battleland.eu/repository/central/")
                credentials {
                    username = System.getenv("NEXUS_USERNAME")
                    password = System.getenv("NEXUS_PASSWORD")
                }
            }
        }
    }
}