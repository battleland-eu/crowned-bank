plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "1.3.4"
    id("xyz.jpenilla.run-paper") version "1.0.6"
    id("net.minecrell.plugin-yml.bukkit") version "0.5.1"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "io.freefair.lombok" version "6.3.0"

    id "maven-publish"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

group 'eu.battleland.crownedbank'
version '1.0-dev'

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven { url 'https://jitpack.io' } // VaultAPI
}

dependencies {
    // in-house
    implementation(project(":api"))

    // cloud
    implementation 'cloud.commandframework:cloud-paper:1.7.0'
    implementation 'cloud.commandframework:cloud-brigadier:1.7.0'

    // vault
    compileOnly ("com.github.MilkBowl:VaultAPI:1.7") {
        exclude group: 'org.bukkit'
    }

    // papi
    compileOnly 'me.clip:placeholderapi:2.11.2'

    // adventure
    implementation "net.kyori:adventure-text-minimessage:4.11.0"

    // paperweight
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.18.1-R0.1-SNAPSHOT")
}


shadowJar {
    archiveFileName = "crownedbank-paper.jar"
    relocate 'cloud.commandframework', 'eu.battleland.crownedbank.libs.cloud.commandframework'
}

tasks.named("build") { dependsOn(tasks.named("shadowJar")) }

bukkit {
    main = "eu.battleland.crownedbank.paper.PaperPlugin"
    name = "CrownedBankPaper"
    apiVersion = "1.18"
    softDepend = Arrays.asList("PlaceholderAPI", "Vault")
    permissions {
        'crownedbank.admin' {
            description = 'Administrator permission.'
            setDefault('OP')
        }
        'crownedbank.player' {
            description = 'Player permission.'
            setDefault('TRUE')
        }
    }
}

publishing {
    publications {
        gpr(MavenPublication) {
            artifactId = 'crowned-bank-paper'
            from components.java
        }
    }
    publishing {
        repositories {
            maven {
                name = "central"
                url = uri("https://nexus.battleland.eu/repository/central/")
                credentials {
                    username = System.getenv("NEXUS_USERNAME")
                    password = System.getenv("NEXUS_PASSWORD")
                }
            }
        }
    }
}

tasks {
    runServer {
        minecraftVersion("1.19")
    }
}